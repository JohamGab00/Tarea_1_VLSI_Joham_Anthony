.fo04.sp

.param SUPPLY=1.8
.param NMOS=220n
.param PMOS=440n
.param H=3
.option PARHIER = LOCAL
.option PORT_VOLTAGE_SCALE_TO_2X = 1
.option S_ELEM_CACHE_DIR = "/home/Cordoba_Wright_I_2024_vlsi/.synopsys_custom/sparam_dir"
.option PVA_OUTPUT_DIR = "/home/Cordoba_Wright_I_2024_vlsi/.synopsys_custom/pva_dir"

.option WDF=1
.temp 25
.lib '/mnt/vol_NFS_rh003/Est_VLSI_I_2024/Leiva_Valverde_I_2024_vlsi/Tarea01/Hspice/lp5mos/xt018.lib' tm
.lib '/mnt/vol_NFS_rh003/Est_VLSI_I_2024/Leiva_Valverde_I_2024_vlsi/Tarea01/Hspice/lp5mos/param.lib' 3s
.lib '/mnt/vol_NFS_rh003/Est_VLSI_I_2024/Leiva_Valverde_I_2024_vlsi/Tarea01/Hspice/lp5mos/config.lib' default

********************************************************************************
* Subcircuit
*********************************************************************************
.global gnd vdd
.subckt inv a y N=NMOS P=PMOS

xm0 y a gnd gnd ne w='N' l=180n as=1.056e-13 ad=1.056e-13 ps=1.4e-06 pd=1.4e-06
+ nrs=1.22727 nrd=1.22727 m='1*1' par1='1*1' xf_subext=0
xm1 y a vdd vdd pe w='P' l=180n as=2.112e-13 ad=2.112e-13 ps=1.84e-06 pd=1.84e-06
+ nrs=0.613636 nrd=0.613636 m='1*1' par1='1*1' xf_subext=0
.ends

*********************************************************************************
* Simulation netlist
*********************************************************************************

Vdd vdd gnd 'SUPPLY'
Vin a gnd PULSE 0 'SUPPLY' 2ns 1ps 1ps 4ns 8.4ns
X1 a b inv * shape input waveform
X2 b c inv M='H' * reshape input waveform 4
X3 c d inv M='H**2' * device under test   16
X4 d e inv M='H**3' * load                64
X5 e f inv M='H**4' * load on load        254


*----------------------------------------------------------------------
* Stimulus
*----------------------------------------------------------------------

.tran 5p 10n start=0

.measure relacion param='PM/NM'
.measure tpdr * rising prop delay
+ TRIG v(c) VAL='SUPPLY/2' FALL=1
+ TARG v(d) VAL='SUPPLY/2' RISE=1

.measure tpdf * falling prop delay
+ TRIG v(c) VAL='SUPPLY/2' RISE=1
+ TARG v(d) VAL='SUPPLY/2' FALL=1

.measure tpd param='(tpdr+tpdf)/2' * average prop delay

.measure trise * rise time
+ TRIG v(d) VAL='0.2*SUPPLY' RISE=1
+ TARG v(d) VAL='0.8*SUPPLY' RISE=1

.measure tfall * fall time
+ TRIG v(d) VAL='0.8*SUPPLY' FALL=1
+ TARG v(d) VAL='0.2*SUPPLY' FALL=1



.option opfile=1 split_dp=1
.option probe=1
.probe tran v(*) level=1
*.probe tran i(*) level=2



.end
